#!/bin/csh -f

# ====================== CCTMv5.1 Run Script ====================== 
# Usage: run.cctm >&! cctm_D502a.log &                                
#
# To report problems or request help with this script/program:        
#             http://www.cmascenter.org
# =================================================================== 
 
#> Source the config.cmaq file to set the run environment
 source /opt/cmaq/CMAQv5.0.2_notpollen//scripts/config.cmaq
 
#> Check that M3DATA is set:
 if ( ! -e $M3DATA ) then
    echo "   $M3DATA path does not exist"
    exit 1
    endif
 echo " "; echo " Input data path, M3DATA set to $M3DATA"; echo " "
 
 set PROC     = serial #> serial or mpi
 set APPL     = CH4only
 set CFG = openmethane
 set MECH = CH4only
 set EXEC   = CCTM_${APPL}_${EXEC_ID}

 
#> horizontal domain decomposition
if ( $PROC == 'serial' ) then
   setenv NPCOL_NPROW "1 1"; set NPROCS   = 1 # single processor setting
else
   ## if on the normal/express queue, use:
   # setenv NPCOL_NPROW "4 4"; set NPROCS   = 16
   
   setenv NPCOL_NPROW "4 6"; set NPROCS   = 24
endif


#> Set the working directory
 set BASE     = $M3HOME/scripts/cctm
 set BLD      = ${BASE}/BLD_${APPL}

 cd $BASE; date; echo "    "; set echo

#> timestep run parameters

 set STDATE = 2022203       # beginning date (YYYYJJJ)
 set STTIME = 000000       # beginning GMT time (HHMMSS)
 set NSTEPS = 240000       # time duration (HHMMSS) for this run
 set TSTEP = 010000        # output time step interval (HHMMSS, e.g. 010000 for hourly output)
 set YEAR = 2022         # YYYY
 set YR = 22           # YY
 set MONTH = 07        # MM
 set DAY = 22          # DD
 set YMD = ${YEAR}${MONTH}${DAY}
 
# =====================================================================
# CCTM Configuration Options
# =====================================================================

#setenv LOGFILE $BASE/$APPL.log  #> log file name; uncomment to write standard output to a log, otherwise write to screen

 setenv GRID_NAME openmethane           #> check GRIDDESC file for GRID_NAME options
 setenv GRIDDESC /opt/project/data/mcip//2022-07-22/d01/GRIDDESC    #> grid description file 

setenv CONC_SPCS "CH4" #> CONC file species; comment or set to "ALL" to write all species to CONC
#setenv CONC_BLEV_ELEV " 1 4" #> CONC file layer range; comment to write all layers to CONC
#setenv CONC_BLEV_ELEV " 1 29" #> CONC file layer range; comment to write all layers to CONC
setenv CONC_BLEV_ELEV " 1 32" #> CONC file layer range; comment to write all layers to CONC
#setenv CONC_BLEV_ELEV "ALL" #> CONC file layer range; comment to write all layers to CONC

setenv AVG_CONC_SPCS "CH4" #> ACONC file species; comment or set to "ALL" to write all species to ACONC
setenv ACONC_BLEV_ELEV " 1 1"  #> ACONC file layer range; comment to write all layers to ACONC
#setenv ACONC_END_TIME Y #> override default beginning ACON timestamp [ default: N ]

setenv EXECUTION_ID $EXEC    #> define the model execution id

#> Sychronization Time Step Options
setenv CTM_MAXSYNC 720       #> max sync time step (sec) [ default: 720 ]
setenv CTM_MINSYNC  60       #> min sync time step (sec) [ default: 60 ]
setenv SIGMA_SYNC_TOP 0.7    #> top sigma level thru which sync step determined [ default: 0.7 ] 

#> Science Options
setenv CTM_WB_DUST N         #> use inline windblown dust emissions [ default: Y ]
setenv CTM_ERODE_AGLAND N    #> use agricultural activity for windblown dust [ default: N ]; ignore if CTM_WB_DUST = N
setenv CTM_WBDUST_BELD USGS #> landuse database for identifying dust source regions [ default: BELD3 ]; ignore if CTM_WB_DUST = N 
setenv CTM_LTNG_NO N         #> turn on lightning NOx [ default: N ]
setenv CTM_WVEL N            #> save derived vertical velocity component to conc file [ default: N ]
setenv KZMIN Y               #> use Min Kz option in edyintb [ default: Y ], otherwise revert to Kz0UT
setenv CTM_ILDEPV Y          #> calculate in-line deposition velocities [ default: Y ]
setenv CTM_MOSAIC N          #> landuse specific deposition velocities [ default: N ]
setenv CTM_ABFLUX N          #> Ammonia bi-directional flux for in-line deposition velocities [ default: N ]; ignore if CTM_ILDEPV = N
setenv CTM_HGBIDI N          #> Mercury bi-directional flux for in-line deposition velocities [ default: N ]; ignore if CTM_ILDEPV = N
setenv CTM_SFC_HONO N        #> Surface HONO interaction [ default: Y ]; ignore if CTM_ILDEPV = N
setenv CTM_BIOGEMIS N        #> calculate in-line biogenic emissions [ default: N ]
setenv CTM_PT3DEMIS N        #> calculate in-line plume rise for elevated point emissions [ default: N ]

#> I/O Controls
setenv IOAPI_LOG_WRITE F     #> turn on excess WRITE3 logging [ options: T | F ]
setenv FL_ERR_STOP N         #> stop on inconsistent input files
setenv PROMPTFLAG F          #> turn on I/O-API PROMPT*FILE interactive mode [ options: T | F ]
setenv IOAPI_OFFSET_64 YES   #> support large timestep records (>2GB/timestep record) [ options: YES | NO ]

#> Diagnostic Output Flags
setenv CTM_CKSUM Y           #> write cksum report [ default: Y ]
setenv CLD_DIAG Y            #> write cloud diagnostic file [ default: N ]
setenv CTM_AERDIAG N         #> aerosol diagnostic file [ default: N ]
setenv CTM_PHOTDIAG N        #> photolysis diagnostic file [ default: N ]
setenv CTM_SSEMDIAG N        #> sea-salt emissions diagnostic file [ default: N ]
setenv CTM_DUSTEM_DIAG N     #> windblown dust emissions diagnostic file [ default: N ]; ignore if CTM_WB_DUST = N
setenv CTM_DEPV_FILE N       #> write diagnostic file for deposition velocities [ default: N ]
setenv LTNGDIAG N            #> write lightning diagnostic file [ default: N ]
setenv B3GTS_DIAG N          #> beis mass emissions diagnostic file [ default: N ]
setenv PT3DDIAG N            #> optional 3d point source emissions diagnostic file [ default: N]; ignore if CTM_PT3DEMIS = N
setenv PT3DFRAC N            #> optional layer fractions diagnostic (play) file(s) [ default: N]; ignore if CTM_PT3DEMIS = N
setenv REP_LAYER_MIN -1      #> Minimum layer for reporting plume rise info [ default: -1 ]

#> MPI Optimization Flags 
setenv MPI_SM_POOL 16000     #> increase shared memory pool in case many MPI_SEND headers
setenv MP_EAGER_LIMIT 65536  #> set MPI message passing buffer size to max
setenv MP_SINGLE_THREAD yes  #> optimizate for single threaded applications [ default: no ]
setenv MP_STDOUTMODE ordered #> order output by the processor ID 
setenv MP_LABELIO yes        #> label output by processor ID [ default: no ]
setenv MP_SHARED_MEMORY yes  #> force use of shared memory for tasks on same node [ default: no ]
setenv MP_ADAPTER_USE shared #> share the MP adapter with other jobs 
setenv MP_CPU_USE multiple   #> share the node with multiple users/jobs
setenv MP_CSS_INTERRUPT yes  #> specify whether arriving packets generate interrupts [ default: no ]

set DISP = delete            #> [ delete | update | keep ] existing output files

# =====================================================================
#> Input/Output Directories
# =====================================================================

set ICpath = /opt/project/data/cmaq//2022-07-22/d01      #> initial conditions input directory 
set BCpath = /opt/project/data/cmaq//2022-07-22/d01      #> boundary conditions input directory
set EMISpath = /opt/project/data/cmaq//2022-07-22/d01      #> surface emissions input directory
set IN_PTpath = $M3DATA/emis      #> elevated emissions input directory (in-line point only)
set IN_LTpath = $M3DATA/lightning #> lightning NOx input directory
set METpath = /opt/project/data/mcip//2022-07-22/d01      #> meteorology input directory 
set JVALpath = /opt/project/data/cmaq//2022-07-22     #> offline photolysis rate table directory
set OMIpath   = /scratch/q90/sa6589/test_Sougol/shared_Sougol/raw/phot  #> ozone columne data for the photolysis model
set LUpath = /opt/project/data/mcip//2022-07-22/d01      #> BELD landuse data for windblown dust model
set SZpath = /opt/project/data/cmaq/     #> Surf zone file for in-line seasalt emissions

set OUTDIR = /opt/project/data/cmaq//2022-07-22/d01       #> output file directory

# =====================================================================
#> Input Files
# =====================================================================

#> Initial conditions
set ICFILE = ICON.d01.openmethane.CH4only.nc

#> Boundary conditions
set BCFILE = BCON.d01.openmethane.CH4only.nc

#> Off-line photolysis rates 
set JVALfile  = JTABLE_${STDATE}

#> Ozone column data
set OMIfile   = OMI.dat

#> MCIP meteorology files 
set EXTN = 220701_aust-test
setenv GRID_DOT_2D $METpath/GRIDDOT2D_${EXTN}
setenv GRID_CRO_2D $METpath/GRIDCRO2D_${EXTN}
setenv MET_CRO_2D $METpath/METCRO2D_${EXTN}
setenv MET_CRO_3D $METpath/METCRO3D_${EXTN}
setenv MET_DOT_3D $METpath/METDOT3D_${EXTN}
setenv MET_BDY_3D $METpath/METBDY3D_${EXTN}

#> Emissions files 

if ( $CTM_PT3DEMIS == 'N' ) then
   set EMISfile = Allmerged_emis_2022-07-22_d01.nc #> Offline 3d emissions file name
else
   #> In-line emissions configuration
#   set STKCASEG = 12US1_G20_CB05E51
#   set STKCASEE = 12US1_cb05e51_G20_CB05E51
#   set CASE = 12CalnexBench_cb05e51_G20_CB05E51
   set EMISfile  = emis_mole_all_${YMD}_${CASE}.ncf #> Surface emissions
   setenv NPTGRPS 5          #> Number of elevated source groups

   setenv STK_GRPS_01 $IN_PTpath/stack_groups_ptnonipm_${STKCASEG}.ncf
   setenv STK_GRPS_02 $IN_PTpath/stack_groups_ptegu_${STKCASEG}.ncf
   setenv STK_GRPS_03 $IN_PTpath/stack_groups_othpt_${STKCASEG}.ncf
   setenv STK_GRPS_04 $IN_PTpath/stack_groups_ptfire_${YMD}_${STKCASEG}.ncf
   setenv STK_GRPS_05 $IN_PTpath/stack_groups_pt_oilgas_${STKCASEG}.ncf
   setenv LAYP_STTIME $STTIME
   setenv LAYP_NSTEPS $NSTEPS

   setenv STK_EMIS_01 $IN_PTpath/inln_mole_ptnonipm_${YMD}_${STKCASEE}.ncf
   setenv STK_EMIS_02 $IN_PTpath/inln_mole_ptegu_${YMD}_${STKCASEE}.ncf
   setenv STK_EMIS_03 $IN_PTpath/inln_mole_othpt_${YMD}_${STKCASEE}.ncf
   setenv STK_EMIS_04 $IN_PTpath/inln_mole_ptfire_${YMD}_${STKCASEE}.ncf
   setenv STK_EMIS_05 $IN_PTpath/inln_mole_pt_oilgas_${YMD}_${STKCASEE}.ncf
   setenv LAYP_STDATE $STDATE
endif

#> Lightning NOx configuration
if ( $CTM_LTNG_NO == 'Y' ) then
#   setenv LTNGNO $IN_LTpath/nox_CMAQ-BENCHMARK.35L.$YMD  #> offline calculated lightning NOx
   setenv LTNGNO "InLine"    #> set LTNGNO to "Inline" to activate in-line calculation

#> In-line lightning NOx options
   setenv LTNGPARAM Y        #> use lightning parameter file? [ default: Y ]
   setenv LTNGPARM_FILE $M3DATA/lightning/LTNG_RATIO.$YEAR.$MONTH.12CalnexBench.ioapi #> lightning parameter file; ignore if LTNGPARAM = N
   setenv LTNGOUT $OUTDIR/$EXEC.LTNGDIAG.${CFG}_${YMD} #> lightning diagnostic file; ignore if LTNGDIAG = N
endif

#> In-line biogenic emissions configuration
if ( $CTM_BIOGEMIS == 'N' ) then   
   set GSPROpath = ${M3DATA}/emis
   setenv GSPRO $GSPROpath/gspro_cb05soa_notoxics_cmaq_poc_09nov2007.txt
   set IN_BEISpath = ${M3DATA}/emis
   setenv B3GRD     $IN_BEISpath/b3grd.smoke30_beis361.12CalnexBench.2006NLCD_FIA5.1_norm_foliage_biomass.ncf
   setenv BIOG_SPRO     B10C5 # speciation profile to use for biogenics
   setenv BIOSW_YN      Y     # use frost date switch [ default: Y ]
   setenv BIOSEASON $IN_BEISpath/bioseason.cmaq.2011_12CalnexBench_wetland100.ghrsst.ncf #> ignore season switch file if BIOSW_YN = N
   setenv SUMMER_YN     N     # Use summer normalized emissions? [ default: Y ]
   setenv PX_VERSION    Y     # MCIP is PX version? [ default: N ]
   setenv INITIAL_RUN Y # non-existent or not using SOILINP [ default: N ]; default uses SOILINP
   setenv SOILINP $OUTDIR/$EXEC.SOILINP.${CFG}_${YMD}  # Biogenic NO soil input file; ignore if INITIAL_RUN = Y
endif

#> Windblown dust emissions configuration
if ( $CTM_WB_DUST == 'N' ) then
   # Input variables for BELD3 Landuse option
   setenv DUST_LU_1 $LUpath/beld3_12CalnexBench_output_a.ncf
   setenv DUST_LU_2 $LUpath/beld4_12CalnexBench_output_tot.ncf
   # Input variables for BELD4 Landuse option
   setenv BELD4_LU $LUpath/ 
   # All other Landuse options (USGS24, MODIS_NOAH, MODIS, NLCD50, NLCD40) read the GRID_CRO_2D file
   if ( $CTM_ERODE_AGLAND == 'Y' ) then
      setenv CROPMAP01 ${M3DATA}/crop/BeginPlanting_12CalnexBench
      setenv CROPMAP04 ${M3DATA}/crop/EndPlanting_12CalnexBench
      setenv CROPMAP08 ${M3DATA}/crop/EndHarvesting_12CalnexBench
   endif
endif

#> In-line sea salt emisisions configuration
setenv OCEAN_1 $SZpath/surfzone_d01.nc #> horizontal grid-dependent surf zone file

#> Bidiretional ammonia configuration
if ( $CTM_ABFLUX == 'Y' ) then
   setenv E2C_Soilfile  ${M3DATA}/bidi/2011_12CalnexBench_soil.nc
   setenv E2C_Fertfile  ${M3DATA}/bidi/2011_12CalnexBench_time${YMD}.nc
   setenv B4LU_file     ${M3DATA}/bidi/beld4_12CalnexBench_2006nlcd.ncf    
   setenv E2C_SOIL ${E2C_Soilfile}
   setenv E2C_FERT ${E2C_Fertfile}
   setenv BELD4_LU ${B4LU_file}
endif

# =====================================================================
#> Output Files
# =====================================================================

#> set output file name extensions
 setenv CTM_APPL ${CFG}_${YMD} 
#> set output file names
 set CONCfile  = $EXEC.CONC.${CTM_APPL}               # CTM_CONC_1
 set ACONCfile = $EXEC.ACONC.${CTM_APPL}              # CTM_ACONC_1
 set CGRIDfile = $EXEC.CGRID.${CTM_APPL}              # CTM_CGRID_1
 set ASXfile   = $EXEC.MEDIA_CONC.${CTM_APPL}         # MEDIA_CONC
 set DD1file   = $EXEC.DRYDEP.${CTM_APPL}             # CTM_DRY_DEP_1
 set DV1file   = $EXEC.DEPV.${CTM_APPL}               # CTM_DEPV_DIAG
 set PT1file   = $EXEC.PT3D.${CTM_APPL}               # CTM_PT3D_DIAG
 set BIO1file  = $EXEC.B3GTS_S.${CTM_APPL}            # B3GTS_S
 set SOIL1file = $EXEC.SOILOUT.${CTM_APPL}            # SOILOUT
 set WD1file   = $EXEC.WETDEP1.${CTM_APPL}            # CTM_WET_DEP_1
 set WD2file   = $EXEC.WETDEP2.${CTM_APPL}            # CTM_WET_DEP_2
 set AV1file   = $EXEC.AEROVIS.${CTM_APPL}            # CTM_VIS_1
 set AD1file   = $EXEC.AERODIAM.${CTM_APPL}           # CTM_DIAM_1
 set RJ1file   = $EXEC.PHOTDIAG1.${CTM_APPL}          # CTM_RJ_2
 set RJ2file   = $EXEC.PHOTDIAG2.${CTM_APPL}          # CTM_RJ_2
 set SSEfile   = $EXEC.SSEMIS.$CTM_APPL               # CTM_SSEMIS_1
 set DSEfile   = $EXEC.DUSTEMIS.$CTM_APPL             # CTM_DUST_EMIS_1
 set PA1file   = $EXEC.PA_1.${CTM_APPL}               # CTM_IPR_1
 set PA2file   = $EXEC.PA_2.${CTM_APPL}               # CTM_IPR_2
 set PA3file   = $EXEC.PA_3.${CTM_APPL}               # CTM_IPR_3
 set IRR1file  = $EXEC.IRR_1.${CTM_APPL}              # CTM_IRR_1
 set IRR2file  = $EXEC.IRR_2.${CTM_APPL}              # CTM_IRR_2
 set IRR3file  = $EXEC.IRR_3.${CTM_APPL}              # CTM_IRR_3
 set DEPVFSTfile = $EXEC.DEPVFST.${CTM_APPL}          # CTM_DEPV_FST
 set DEPVMOSfile = $EXEC.DEPVMOS.${CTM_APPL}          # CTM_DEPV_MOS
 set DDFSTfile = $EXEC.DDFST.${CTM_APPL}              # CTM_DRY_DEP_FST
 set DDMOSfile = $EXEC.DDMOS.${CTM_APPL}              # CTM_DRY_DEP_MOS
#> In-line biogenic emissions output files
if ( $CTM_BIOGEMIS == 'Y' ) then 
   setenv B3GTS_S $OUTDIR/$EXEC".B3GTS_S".${CTM_APPL}
   setenv SOILOUT $OUTDIR/$EXEC".SOILOUT".${CTM_APPL}  # Biogenic NO soil output file
endif

#> set floor file (neg concs)
setenv FLOOR_FILE $BASE/FLOOR_${CTM_APPL}

#> create output directory 
if ( ! -d "$OUTDIR" ) mkdir -p $OUTDIR

#> look for existing log files
                              
 set test = `ls CTM_LOG_???.${CTM_APPL}`
 if ( "$test" != "" ) then
    if ( $DISP == 'delete' ) then
       echo " ancillary log files being deleted"
       foreach file ( $test )
          echo " deleting $file"
          rm $file
          end
       else
       echo "*** Logs exist - run ABORTED ***"
       exit 1
       endif
    endif

#> for the run control ...

setenv CTM_STDATE      $STDATE
setenv CTM_STTIME      $STTIME
setenv CTM_RUNLEN      $NSTEPS
setenv CTM_TSTEP       $TSTEP
setenv EMIS_1 $EMISpath/$EMISfile
setenv INIT_GASC_1 $ICpath/$ICFILE
setenv INIT_AERO_1 $INIT_GASC_1
setenv INIT_NONR_1 $INIT_GASC_1
setenv INIT_TRAC_1 $INIT_GASC_1
setenv BNDY_GASC_1 $BCpath/$BCFILE
setenv BNDY_AERO_1 $BNDY_GASC_1
setenv BNDY_NONR_1 $BNDY_GASC_1
setenv BNDY_TRAC_1 $BNDY_GASC_1
setenv OMI $OMIpath/$OMIfile
setenv XJ_DATA $JVALpath/$JVALfile
set TR_DVpath = $METpath
set TR_DVfile = $MET_CRO_2D
 
#> species defn & photolysis
setenv gc_matrix_nml ${BLD}/GC_$MECH.nml
setenv ae_matrix_nml ${BLD}/AE_$MECH.nml
setenv nr_matrix_nml ${BLD}/NR_$MECH.nml
setenv tr_matrix_nml ${BLD}/Species_Table_TR_0.nml
 
#> check for photolysis input data
setenv CSQY_DATA ${BLD}/CSQY_DATA_$MECH

if (! (-e $CSQY_DATA ) ) then
   echo " $CSQY_DATA  not found "
   exit 1
endif


#>- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

 source $BASE/outck.q

 ls -l $BLD/$EXEC; size $BLD/$EXEC
# unlimit
# limit

#> Executable call for single PE, uncomment to invoke
if ( $PROC == 'serial' ) then
 time  $BLD/$EXEC
else
#> Executable call for multi PE, configure for your system 
 ## set MPIRUN = mpirun
 ## set MPIRUN = /mnt/appsource/intel_studio/intel_studio-2016.3/compilers_and_libraries_2016.3.210/linux/mpi/intel64/bin/mpirun
 set MPIRUN = mpirun
 ## time $MPIRUN -r ssh -np $NPROCS $BLD/$EXEC
 ## /usr/bin/time $MPIRUN -r ssh -np $NPROCS $BLD/$EXEC
 time $MPIRUN -np $NPROCS $BLD/$EXEC

endif
 
 date
 exit
